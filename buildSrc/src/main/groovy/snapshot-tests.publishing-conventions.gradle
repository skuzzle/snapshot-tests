plugins {
    id("snapshot-tests.base-conventions")
    id("maven-publish")
    id("signing")
}

tasks.withType(Sign).configureEach {
    onlyIf { isContinuousIntegrationEnvironment }
}

tasks.withType(PublishToMavenLocal).configureEach {
    dependsOn(tasks.build)
}

signing {
    def signingKey = base64Decode(findProperty("base64EncodedAsciiArmoredSigningKey"))
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign(publishing.publications)
}

def base64Decode(encodedString){
    if(encodedString != null) {
        byte[] decoded = encodedString.decodeBase64()
        String decode = new String(decoded)
        return decode
    }
    return null;
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                url = 'https://github.com/skuzzle/snapshot-tests'
                scm {
                    connection = "scm:git:git://github.com/skuzzle/snapshot-tests"
                    developerConnection = "scm:git:git://github.com/skuzzle/snapshot-tests"
                    url = "https://github.com/skuzzle/snapshot-tests"
                }
                licenses {
                    license {
                        name = "The MIT License (MIT)"
                        url = "http://opensource.org/licenses/MIT"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "simont"
                        name = "Simon Taddiken"
                        email = "simon@taddiken.net"
                    }
                }
            }
        }
    }
}