plugins {
    id("snapshot-tests.base-conventions")
    id("java-library")
    id("jacoco")
}

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs << '-parameters'
}

javadoc {
    options.tags = [
            "apiNote:a:API Note:",
            "implSpec:a:Implementation Requirements:",
            "implNote:a:Implementation Note:"]
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(libs.apiguardian)

    implementation platform(libs.junit.bom)
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation(libs.equalsverifier)
    testImplementation(libs.assertj.core)
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name': provider { project.findProperty("automaticModuleName") },
                "Created-By": "${System.getProperty("java.version")} (${System.getProperty("java.vendor")} ${System.getProperty("java.vm.version")})",
                "Specification-Title": project.name,
                "Specification-Version": project.version - "-SNAPSHOT",
                "Implementation-Title": project.name,
                "Implementation-Version": project.version
        )
    }
}

if (project.getPluginManager().hasPlugin("maven-publish")) {
    publishing {
        publications.named("maven") {
            from(components.java)
            pom {
                description("Module \"${project.name}\" of snapshot-tests")
            }
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}